@name Control Knob
@inputs Reset Mult
@outputs Number User:entity MoveMult
@persist Move User:entity
@model models/cheeze/buttons2/up_small.mdl

if(first()){
    Move = 0
    Number = 0
    
    MoveMult = 1
    
    User = noentity()
}

if(~Mult){
    MoveMult = Mult
}

event keyPressed(Ply:entity,Key:string,Down:number,_:string){
    if(Ply:aimEntity() == entity() & Ply:pos():distance(entity():pos()) <= 100){
        if(!User:isValid() | User == noentity()){
            User = Ply
        }
        
        if(User:isValid()){
            if(Key == "e" & Down == 1){
                Move = 1
            }
            elseif(Key == "r" & Down == 1){
                Move = 2
            }
            
            if(Down == 0){
                Move = 0
                User = noentity()
            }
        }
        else{
            Move = 0
            User = noentity()
        }
    }
}

event tick(){
    if(Move == 1){ #Anti-Clockwise
        entity():soundPlay(1,0.07,"buttons/spark2.wav")
        entity():setAng(entity():toWorld(ang(0,MoveMult,0)))
        Number = Number+MoveMult
    }
    elseif(Move == 2){ #Clockwise
        entity():soundPlay(1,0.07,"buttons/spark2.wav")
        entity():setAng(entity():toWorld(ang(0,-MoveMult,0)))
        Number = Number-MoveMult
    }
    
    if(Move != 0){
        if(User:isValid() & User:aimEntity() != entity()){
            Move = 0
            User = noentity()
        }
    }
}

if(~Reset & Reset){
    Number = 0
}
