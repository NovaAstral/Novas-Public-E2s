@name Tardis Vortex Drive v2.1
@inputs Parent:entity WebAudioSF:wirelink SoundPlayingWebSF Throttle FastReturn FastMode VeryFastMode DisableVortexMode EngineRelease Cloak DestIn:vector AngleOffset:angle AngIn:angle ToiMode
@outputs Dematted Moving PAlpha State [Destination,PrevDestination]:vector [Angle,PrevAngle]:angle
@persist State AnimSpeed DematWait RematWait [DematAlphaTbl,MatAlphaTbl]:table Snd1:string AudioPos:vector CloakAlpha Cloaking DupeWait [Destination,PrevDestination]:vector [Angle,PrevAngle]:angle
@persist [DematSnd,MatSnd,FastDematSnd,FastMatSnd,ForcedDematSnd,ForcedMatSnd,ForcedFastDematSnd,ForcedFastMatSnd,FailSnd,CloakSnd,DeCloakSnd,VeryFastDematSnd,VeryFastMatSnd]:string
#veryfastmode forced demat timing is wrong
#either change the dematwait when forced, or change the sound (change the sound probably better)

function void playsound(Audio:string,Vec:vector,Vol,FadeS,FadeE,MonoEnable){
    WebAudioSF["URL",string] = Audio
    WebAudioSF["Pos",vector] = Vec
    WebAudioSF["Volume",number] = Vol
    WebAudioSF["FadeStart",number] = FadeS
    WebAudioSF["FadeEnd",number] = FadeE
    WebAudioSF["Mono",number] = MonoEnable
        
    timer("playsnd",200)
    timer("noplaysnd",250)
}
    
if(first() | dupefinished()){
    AnimSpeed = 30 #15 is fastest speed possible
    
    DematSnd = "https://cloud.luna-code.net/s/PrM4qyTbQ57QPoH/download"
    MatSnd = "https://cloud.luna-code.net/s/QL0hqCJpkhRqYpc/download"
    FastDematSnd = "https://cloud.luna-code.net/s/onXII5pRGHEvYfV/download"
    FastMatSnd = "https://cloud.luna-code.net/s/5P2W9IA6o23AVMF/download"
    ForcedDematSnd = "https://cloud.luna-code.net/s/Mk5sjFH1uAlVr8Z/download"
    ForcedMatSnd = "https://cloud.luna-code.net/s/QE1hClvs3l2f1K7/download"
    ForcedFastDematSnd = "https://cloud.luna-code.net/s/vsNhCHGNgpbEhrh/download"
    ForcedFastMatSnd = "https://cloud.luna-code.net/s/Dl0ZREBZOlqBT9c/download"
    VeryFastDematSnd = "https://cloud.luna-code.net/s/jeYfNafnTnMflwj/download"
    VeryFastMatSnd = "https://cloud.luna-code.net/s/VSiIeZYzath04p8/download"
    FailSnd = "https://cloud.luna-code.net/s/7Rol1sDci7tJs4q/download"
    CloakSnd = "https://cloud.luna-code.net/s/51J6CQk9aQExNWR/download"
    DeCloakSnd = "https://cloud.luna-code.net/s/sflsHGsRLRt018j/download"
    
    
    if(FastMode == 0){
        AnimSpeed = 30
        DematWait = 6000 #how long to wait after the sound starts playing for the demat anim to play
        RematWait = 10000 #how long to wait after the sound starts playing for the remat anim to play
        
        DematAlphaTbl = table(
            150,
            200,
            100,
            150,
            50,
            100,
            0
        )
        
        MatAlphaTbl = table(
            100,
            50,
            150,
            100,
            200,
            150,
            255
        )
    }
    else{ #Fast Mode
        AnimSpeed = 15
        DematWait = 2000
        RematWait = 100
        
        DematAlphaTbl = table(
            150,
            200,
            100,
            150,
            0
        )
        MatAlphaTbl = table(
            100,
            50,
            150,
            100,
            255
        )
    }
    
    #dont touch these
    State = 1
    Dematted = 0
    Moving = 0
    CloakAlpha = 255
    Cloaking = 0
    
    function number checkJammed(){
        if(isJammed(Destination) | isJammed(Parent:pos())){
            return 1
        }
        else{
            return 0
        }
    }

    function number mathApproach(Cur:number,Tar:number,Change:number){
        if(Tar > Cur){
            return clamp(Cur+Change,Cur,Tar)
        }
        elseif(Tar < Cur){
            return clamp(Cur-Change,Tar,Cur)
        }
        else{
            return Cur
        }
    }
    
    function void moveParent(){
        if(Destination != vec(0)){
            Parent:setPos(Destination)
        }
                    
        if(AngleOffset != ang(0)){
            Parent:setAng(Parent:angles() + AngleOffset)
        }
        else{
            Parent:setAng(Angle)
        }
    }
    
    Parent:setAlpha(255)
    PAlpha = 255
    
    DupeWait = 1
    timer("dupewait",5000)
}

if(dupefinished()){
    timer("reset",5000)
}

if(clk("dupewait")){ #workaround for some annoying fuckery
    DupeWait = 0
}

if(clk("reset")){ #other workaround for some annoying fuckery
    reset()
}

if(clk("playsnd")){
    WebAudioSF["Play",number] = 1
}

if(clk("noplaysnd")){
    WebAudioSF["Play",number] = 0
}

if(~FastMode | ~VeryFastMode){
    if(FastMode == 1){
        AnimSpeed = 15
        DematWait = 2000
        RematWait = 100
        
        DematAlphaTbl = table(
            150,
            200,
            100,
            150,
            0
        )
        MatAlphaTbl = table(
            100,
            50,
            150,
            100,
            255
        )
    }
    else{
        AnimSpeed = 30
        DematWait = 6000
        RematWait = 10000
        
        DematAlphaTbl = table(
            150,
            200,
            100,
            150,
            50,
            100,
            0
        )
        
        MatAlphaTbl = table(
            100,
            50,
            150,
            100,
            200,
            150,
            255
        )
    }
    
    if(VeryFastMode == 1){
        AnimSpeed = 15
        DematWait = 0
        RematWait = 0
        
        DematAlphaTbl = table(
            0
        )
        
        MatAlphaTbl = table(
            255
        )
    }
}

function movethebox(){
    if(checkJammed() == 1){
        if(EngineRelease == 1){
            if(Moving == 0){
                if(Dematted == 0){ #demat
                    Moving = 1
                    
                    PrevDestination = Parent:pos()# + vec(0,0,PosOffset)
                    PrevAngle = Parent:angles()
                    
                    timer("sndwait",10)
                    if(FastMode == 0){
                        DematWait = 4000
                    }
                    else{
                        DematWait = 100
                    }
                }
                else{ #remat
                    moveParent()
                    
                    Moving = 1
                    
                    timer("sndwait",10)
                    
                    if(FastMode == 0){
                        RematWait = 8000
                    }
                    else{
                        RematWait = 100
                    }
                }
            }
        }
        else{
            playsound( #cant move sound
                FailSnd, #URL
                Parent:pos(), #Audio Pos
                2, #Volume
                2000, #Fade Start
                1000000000, #Fade End
                0 #Mono
            )
        }
    }
    else{
        if(Moving == 0){
            if(Dematted == 0){ #demat
                Moving = 1
                
                PrevDestination = Parent:pos()# + vec(0,0,PosOffset)
                PrevAngle = Parent:angles()
                
                timer("sndwait",10)
                
                if(VeryFastMode == 1){
                    DematWait = 0
                    RematWait = 0
                }
                else{
                    if(FastMode == 0){
                        DematWait = 6000
                    }
                    else{
                        DematWait = 2000
                    }
                }
            }
            else{ #remat
                moveParent()
                
                Moving = 1
                timer("sndwait",10)
                
                if(VeryFastMode == 1){
                    DematWait = 0
                    RematWait = 0
                }
                else{
                    if(FastMode == 0){
                        RematWait = 10000
                    }
                    else{
                        RematWait = 100
                    }
                }
            }
        }
    }
}

if(~Throttle & Throttle & !first() & DupeWait == 0){
    movethebox()
}

if(~DestIn){
    Destination = DestIn
}

if(~FastReturn & FastReturn & !first() & DupeWait == 0){
    Destination = PrevDestination
    Angle = PrevAngle
    
    DisableVortexMode = 1
    #FastMode = 1
    
    timer("fastreturnwait",100)
}

if(clk("fastreturnwait")){
    movethebox()
}

if(~AngIn){
    Angle = AngIn
}

if(~SoundPlayingWebSF & SoundPlayingWebSF == 1){
    if(Cloaking == 0){
        if(checkJammed() == 1){
            if(EngineRelease == 1){
                if(Dematted == 0){
                    timer("demat",DematWait)
                }
                else{
                    timer("remat",RematWait)
                }
            }
        }
        else{
            if(Dematted == 0){
                timer("demat",DematWait)
            }
            else{
                timer("remat",RematWait)
            }
        }
    }
    else{
        timer("cloaktimer",1)
    }
}

if(~Cloak & !Moving & !Dematted & DupeWait == 0){
    if(Cloak == 1){
        CloakAlpha = 0
        Cloaking = 1
        
        playsound(
            CloakSnd, #cloak
            Parent:pos(), #Audio Pos
            2, #Volume
            2000, #Fade Start
            1000000000, #Fade End
            0 #Mono
        )
    }
    else{
        CloakAlpha = 255
        Cloaking = 1
        playsound(
            DeCloakSnd, #decloak
            Parent:pos(), #Audio Pos
            2, #Volume
            2000, #Fade Start
            1000000000, #Fade End
            0 #Mono
        )
    }
}

if(clk("cloaktimer")){
    timer("cloaktimer",30)

    Parent:setAlpha(mathApproach(Parent:getAlpha(),CloakAlpha,4))
    
    if(Parent:getAlpha() == CloakAlpha){
        stoptimer("cloaktimer")
        Cloaking = 0
    }
}

if(clk("sndwait")){
    if(Dematted == 0){
        if(VeryFastMode == 1){
            Snd1 = VeryFastDematSnd #very fast demat
        }
        else{
            if(FastMode == 1){
                if(checkJammed() == 1 & EngineRelease == 1){
                    Snd1 = ForcedFastDematSnd #fast forced demat
                }
                else{
                    Snd1 = FastDematSnd #fastdemat
                }
            }
            else{
                if(checkJammed() == 1 & EngineRelease == 1){
                    Snd1 = ForcedDematSnd #forced demat
                }
                else{
                    if(ToiMode == 1){
                        Snd1 = "https://cloud.luna-code.net/s/c1bvRDlkaNJuWFC/download" #toi demat
                    }
                    else{
                        Snd1 = DematSnd #demat
                    }
                }
            }
        }
    }
    else{
        if(VeryFastMode == 1){
            Snd1 = VeryFastMatSnd #very fast mat
        }
        else{
            if(FastMode == 1){
                if(checkJammed() == 1 & EngineRelease == 1){
                    Snd1 = ForcedFastMatSnd #forced fast mat
                }
                else{
                    Snd1 = FastMatSnd #fastmat
                }
            }
            else{
                if(checkJammed() == 1 & EngineRelease == 1){
                    Snd1 = ForcedMatSnd #forced mat
                }
                else{
                    if(ToiMode == 1){
                        Snd1 = "https://cloud.luna-code.net/s/QeQnGlV1xYMm3FZ/download" #toi mat
                    }
                    else{
                        Snd1 = MatSnd #mat
                    }
                }
            }
        }
    }
    
    if(Dematted == 0){ #play at box
        AudioPos = Parent:pos()
    }
    else{ #play at dest
        AudioPos = Destination
    }
    playsound(
            Snd1, #URL
            AudioPos, #Audio Pos
            2, #Volume
            2000, #Fade Start
            1000000000, #Fade End
            0 #Mono
        )
}

if(clk("demat")){
    timer("demat",AnimSpeed)
    
    if(VeryFastMode == 1){
        Parent:setAlpha(mathApproach(Parent:getAlpha(),DematAlphaTbl[State,number],10))
    }
    else{
        if(checkJammed() == 1 & EngineRelease == 1){
            Parent:setAlpha(Parent:getAlpha()+randint(-6,6))
        }
        
        Parent:setAlpha(mathApproach(Parent:getAlpha(),DematAlphaTbl[State,number],2))
    }
    
    PAlpha = Parent:getAlpha()
    
    if(Parent:getCollisionGroup() != _COLLISION_GROUP_WORLD){
        Parent:noCollideAll(1)
    }
    
    if(Parent:getAlpha() == DematAlphaTbl[State,number]){
        State = clamp(State+1,1,DematAlphaTbl:count()+1)
    }
    
    if(State >= DematAlphaTbl:count()+1){
        stoptimer("demat")
        State = 1
        Moving = 0
        Dematted = 1
        
        if(DisableVortexMode == 1){
            moveParent()
            
            Moving = 1
            timer("sndwait",10)
        }
    }
}

if(clk("remat")){
    timer("remat",AnimSpeed)
    
    if(VeryFastMode == 1){
        Parent:setAlpha(mathApproach(Parent:getAlpha(),MatAlphaTbl[State,number],10))
    }
    else{
        if(checkJammed() == 1 & EngineRelease == 1){
            Parent:setAlpha(Parent:getAlpha()+randint(-6,6))
        }
        
        Parent:setAlpha(mathApproach(Parent:getAlpha(),MatAlphaTbl[State,number],2))
    }
    
    PAlpha = Parent:getAlpha()
    
    if(Parent:getAlpha() == MatAlphaTbl[State,number]){
        if(FastMode == 1 & State == 5){
            State = 7
        }
        else{
            State = clamp(State+1,1,MatAlphaTbl:count()+1)
        }
    }
    
    if(State >= MatAlphaTbl:count()+1){
        stoptimer("remat")
        State = 1
        Moving = 0
        Dematted = 0
        
        if(Parent:getCollisionGroup() != _COLLISION_GROUP_NONE){
            Parent:noCollideAll(0)
        }
    }
}
