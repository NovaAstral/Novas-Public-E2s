@name Tardis Destination Select
@inputs Activate Exterior:entity [CamCont,PodCont]:wirelink OffsetIn FlightMode Door:entity
@outputs SpeedMult PosOut:vector AngOut:angle Active [ChameleonModel,ChameleonMat]:string ChameleonColor:vector4 Offset Holo:entity
@persist SpeedMult AMoveDelay Active Plys:array Offset SetChameleonDelay

if(first() | dupefinished()){
    holoCreate(1)
    holoModel(1,Exterior:model())
    holoPos(1,Exterior:pos())
    holoAng(1,Exterior:angles())
    holoAlpha(1,0)
    
    Holo = holoEntity(1)
    
    SpeedMult = 30
    AMoveDelay = 0
    SetChameleonDelay = 0
    
    Offset = 0
}

if(~OffsetIn){
    Offset = OffsetIn
}

if(~Activate & !first() & !FlightMode){
    holoDelete(2)
    holoDelete(3)
    if(Activate == 1){
        Plys = players()
        for(I=1,Plys:count()){
            Ply = Plys[I,entity]
            holoVisible(1,Ply,1)
            if(Ply != PodCont["Driver",entity]){
                holoVisible(1,Ply,0)
            }
        }
        
        if(Exterior:model() == "models/sprops/rectangles/size_66/rect_66x66x3.mdl"){ #police box
            Offset = 65
            
            holoModel(1,"models/sprops/cuboids/height48/size_1/cube_48x132x48.mdl")
            holoScale(1,vec(1,1.4,1.4))
            holoPos(1,Exterior:pos()+vec(0,0,65))
            holoAng(1,Exterior:angles()+ang(90,0,0))
            
            holoCreate(2,holoEntity(1):pos()+holoEntity(1):up()*25,vec(1),ang(0),vec(255),"models/sprops/cuboids/height12/size_1/cube_12x12x12.mdl") #forward
            holoParent(2,holoEntity(1))
            
            for(I=1,Plys:count()){
                Ply = Plys[I,entity]
                holoVisible(2,Ply,1)
                if(Ply != PodCont["Driver",entity]){
                    holoVisible(2,Ply,0)
                }
            }
        }
        elseif(Exterior:model() == "models/hunter/tubes/circle2x2.mdl"){ #hl2 junk tardis
            holoModel(1,Exterior:model())
            holoScale(1,vec(4,4,1))
            holoPos(1,Exterior:pos())
            holoAng(1,Exterior:angles())
            
            holoCreate(2,holoEntity(1):pos()+holoEntity(1):up()*115,vec(4,4,1),ang(0),vec4(255,255,255,100),"models/hunter/tubes/circle2x2.mdl")
            holoParent(2,holoEntity(1))
            
            holoCreate(3,Door:pos()+Door:up()*25,vec(1),Door:angles(),vec(255),"models/sprops/cuboids/height12/size_1/cube_12x12x12.mdl") #forward
            holoParent(3,holoEntity(1))
            
            for(I=1,Plys:count()){
                Ply = Plys[I,entity]
                holoVisible(2,Ply,1)
                holoVisible(3,Ply,1)
                if(Ply != PodCont["Driver",entity]){
                    holoVisible(2,Ply,0)
                    holoVisible(3,Ply,0)
                }
            }
        }
        elseif(Exterior:model() == "models/hunter/blocks/cube1x1x1.mdl"){ #mc junk tardis
            Offset = 16.965
            holoModel(1,Exterior:model())
            holoScale(1,vec(2.86,2.86,0.715))
            holoPos(1,Exterior:pos())
            holoAng(1,Exterior:angles())
            
            holoCreate(2,holoEntity(1):pos()+holoEntity(1):up()*51+holoEntity(1):forward()*-85,vec(2.86,2.86,0.715),ang(0,0,90),vec4(255,255,255,100),Exterior:model())
            holoParent(2,holoEntity(1))
            
            holoCreate(3,holoEntity(1):pos()+holoEntity(1):up()*51+holoEntity(1):right()*-85,vec(2.86,2.86,0.715),ang(0,90,90),vec4(255,255,255,100),Exterior:model())
            holoParent(3,holoEntity(1))
            
            holoCreate(4,holoEntity(1):pos()+holoEntity(1):up()*135+holoEntity(1):right()*17+holoEntity(1):forward()*17,vec(0.715,0.715,0.715),ang(0,90,90),vec4(255,255,255,100),Exterior:model())
            holoParent(4,holoEntity(1))
            
            for(I=1,Plys:count()){
                Ply = Plys[I,entity]
                holoVisible(2,Ply,1)
                holoVisible(3,Ply,1)
                holoVisible(4,Ply,1)
                if(Ply != PodCont["Driver",entity]){
                    holoVisible(2,Ply,0)
                    holoVisible(3,Ply,0)
                    holoVisible(4,Ply,0)
                }
            }
        }
        else{
            holoDelete(2)
            holoModel(1,Exterior:model())
            holoScale(1,vec(1))
            holoPos(1,Exterior:pos())
            holoAng(1,Exterior:angles())
        }
        
        holoAlpha(1,150)
        Active = 1
        
        timer("destselect",10)
    }
    else{
        holoAlpha(1,0)
        Active = 0
    }
}


if(clk("destselect")){ #timer because running on tick crashes with 9+ people
    timer("destselect",100)
    
    if(Active == 1){ #Flight / Destination Select
        if(PodCont["W",number] == 1){
            holoPos(1,holoEntity(1):pos() + CamCont["CamAng",angle]:forward()*SpeedMult)
        }
        
        if(PodCont["A",number] == 1){
            if(PodCont["Mouse2",number] == 1){
                holoAng(1,holoEntity(1):angles() + ang(0,SpeedMult,0))
            }
            else{
                holoPos(1,holoEntity(1):pos() + CamCont["CamAng",angle]:right()*-SpeedMult)
            }
        }
        
        if(PodCont["S",number] == 1){
            holoPos(1,holoEntity(1):pos() + CamCont["CamAng",angle]:forward()*-SpeedMult)
        }
        
        if(PodCont["D",number] == 1){
            if(PodCont["Mouse2",number] == 1){
                holoAng(1,holoEntity(1):angles() + ang(0,-SpeedMult,0))
            }
            else{
                holoPos(1,holoEntity(1):pos() + CamCont["CamAng",angle]:right()*SpeedMult)
            }
        }
        
        if(PodCont["Space",number] == 1){ #go up
            holoPos(1,holoEntity(1):pos() + vec(0,0,1)*SpeedMult)
        }
        
        if(PodCont["Duck",number] == 1){ #go down
            holoPos(1,holoEntity(1):pos() + vec(0,0,1)*-SpeedMult)
        }
        
        if(PodCont["Shift",number] == 1 & PodCont["Alt",number] == 1){ #change speed
            SpeedMult = 100
        }
        elseif(PodCont["Shift",number] == 1){
            SpeedMult = 30
        }
        elseif(PodCont["Alt",number] == 1){
            SpeedMult = 2
        }
        else{
            SpeedMult = 10
        }
        
        if(PodCont["R",number] == 1){ #Move the holo to where the camera is looking
            if(AMoveDelay != 1){
                AMoveDelay = 1
                
                holoPos(1,CamCont["HitPos",vector]+vec(0,0,Offset))
                
                timer("aimmovedelay",500)
            }
        }
        
        if(PodCont["Mouse1",number] == 1){ #set the destination to the holo pos/angles
            if(Exterior:model() == "models/sprops/rectangles/size_66/rect_66x66x3.mdl"){
                PosOut = holoEntity(1):pos()+vec(0,0,-65)
                AngOut = holoEntity(1):angles()+ang(-90,0,0)
            }
            else{
                PosOut = holoEntity(1):pos()
                AngOut = holoEntity(1):angles()
            }
            
            holoAlpha(1,0)
            Active = 0
            
            if(holoEntity(2):isValid()){
                holoDelete(2)
                holoDelete(3)
                holoDelete(4)
            }
            
            stoptimer("destselect")
        }
        
        if(PodCont["Noclip",number] == 1 & SetChameleonDelay == 0){ #Set holo to pos and angles of the entity the camera is looking at
            SetChameleonDelay = 1
            timer("aimmovedelay",500)
            
            IgnoreArray = array(Exterior,holoEntity(1))
            
            rangerFilter(IgnoreArray)
            local RangerData = CamCont["Trace",ranger]
            
            local RangerEnt = RangerData:entity()
            
            if(RangerEnt:isValid()){
                if(PodCont["Alt",number] == 1){
                    ChameleonModel = RangerEnt:model()
                    ChameleonMat = RangerEnt:getMaterial()
                    ChameleonColor = RangerEnt:getColor4()
                    
                    timer("unsetmatcol",1000) #so the mat/col of the exterior isn't set if the chameleon is set with other means
                    
                    holoModel(1,RangerEnt:model())
                    
                    Offset = 0
                }
                
                holoPos(1,RangerEnt:pos())
                holoAng(1,RangerEnt:angles())
            }
        }
    }
}

if(clk("aimmovedelay")){
    AMoveDelay = 0
    SetChameleonDelay = 0
}

if(clk("unsetmatcol")){
    ChameleonMat = ""
    ChameleonColor = vec4(0)
}
