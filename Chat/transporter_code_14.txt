@name Transporter Code 14
@inputs Pad:entity
@outputs E:entity A
@persist PadW:wirelink

#This attempts to emulate Transporter Code 14 from -
#- Star Trek The Next Generation: Season 3 Episode 19 "Captains Holiday"

if(first()){
    PadW = Pad:wirelink()
}

if(~Pad & Pad){
    PadW = Pad:wirelink()
}

event chat(SaidPly:entity, Said:string,_){
    local OLS = Said:lower():explode(" ")
    
    if(SaidPly == owner()){
        if(OLS[1,string] == "/c14"){
            E = owner():aimEntity()
            timer("ptarget",10)
            
            if(E:type() == "player"){
                printColor(vec(255,95,215)," [C14] ",vec(230,110,0),"You cannot destroy other players!")
                owner():soundPlay(4,1,"st/shuttlecraft/alert_critical.mp3")
                stopAllTimers()
            }
        }
    }
}

if(clk("ptarget")){
    PadW["Target1",entity] = E
    timer("beam",10)
    holoCreate(1,E:pos())
    holoAlpha(1,0)
}

if(clk("beam")){
    PadW["Beam to pads",number] = 1
    timer("test",10)
}

if(clk("test")){
    
    if(PadW["Energising",number] == 0){
        printColor(vec(255,95,215)," [C14] ",vec(230,110,0),"Entity can't be energised, aborting code 14")
        owner():soundPlay(1,1,"st/shuttlecraft/computer_deny.wav")
        stopAllTimers()
        holoDelete(1)
    }
    else{
        printColor(vec(255,95,215)," [C14] ",vec(230,110,0),"Initiating Transporter Code 14 on aimed entity.")
        timer("explode",1000)
        timer("col",10)
        owner():soundPlay(2,1,"st/shuttlecraft/computer_work.mp3")
        A = 1
    }
}

if(clk("explode")){
    holoEntity(1):soundPlay(3,5,"ship_core/explode1.wav")
    timer("entdel",500)
    A = 0
}

if(clk("entdel")){
    E:propDelete()
    timer("holodel",4000)
}

if(clk("holodel")){
    holoDelete(1)
    stopAllTimers()
}
if(clk("col")){
    timer("col",200)
    E:setColor(randint(0,255),randint(0,255),randint(0,255))
}
