@name Chat Target
@inputs
@outputs TargetPos:vector TargetEnt:entity TargetOwner:entity TargetLocked
@persist Prefix:string FEnt:entity FEnts:array Color:vector AutoTarget AutoTargetPrint

#include "Novas-Public-E2s/library/nova_lib"

if(first() | dupefinished()){
    Prefix = "/t" #Target Prefix
    
    AutoTarget = 1 #If Target destroyed, try to target another of the same entity owned by the same person
    
    AutoTargetPrint = 0 #Print in chat when AutoTarget targets a new entity
    
    timer("functionsetup",10)
}

if(clk("functionsetup")){
    FEnts = array()
    
    function void target(Ent:entity,Print){
        TargetEnt = Ent
        TargetPos = Ent:boxCenterW()
        
        TargetLocked = 1
        
        timer("target",1)
        
        if(Ent:owner():name() == ""){
            TargetOwner = noentity()
            Color = vec(255)
        }
        else{
            TargetOwner = Ent:owner()
            Color = teamColor(Ent:owner():team())
        }
        
        if(Print == 1){
            if(Ent:isPlayer()){
                printColor(ColPink,"[Target] ",ColOng,"Targetting ",teamColor(Ent:team()),Ent:name())
            }
            else{
                printColor(ColPink,"[Target] ",ColOng,"Targetting ",ColGrn,Ent:type(),ColOng," Owned by: ",Color,Owner)
            }
        }
    }
}

if(clk("target")){
    timer("target",10)
    
    if(TargetEnt:isValid()){
        TargetPos = TargetEnt:boxCenterW()
            
        TargetLocked = 1
    }
    else{
        if(AutoTarget == 1){
            if(FEnts:count() > 0){
                TargetEnt = FEnts:popEntity()
                
                if(TargetEnt:isValid()){
                    target(TargetEnt,AutoTargetPrint)
                }
            }
            else{
                printColor(ColPink,"[Target] ",ColOng,"Target Lost! Likely destroyed.")
                    
                TargetLocked = 0
                    
                stoptimer("target")
            }
        }
        else{
            printColor(ColPink,"[Target] ",ColOng,"Target Lost! Likely destroyed.")
            
            TargetLocked = 0
            
            stoptimer("target")
        }
    }
}

event chat(SaidPly:entity, Said:string,_){
    local OLS = Said:lower():explode(" ")
    
    if(SaidPly == owner()){
        if(OLS[1,string] == Prefix){
            hideChat(1)
            
            if(OLS[2,string] == "help"){
                printColor(ColPink,"[Target] ",ColOng,"Chat Target Help:")
                printColor(ColPink,"[Target] ",ColOng,"If you're seeing this, I haven't made a help file yet oops")
            }
            elseif(OLS[2,string] == "list"){
                printColor(ColPink,"[Target] ",ColOng,"Target List:")
                printColor(ColOng,TarType:keys():concat(", "))
            }
            elseif(OLS[2,string] == "this" | OLS[2,string] == "that"){
                if(owner():aimEntity():isValid()){
                    target(owner():aimEntity(),1)
                }
                else{
                    printColor(ColPink,"[Target] ",ColOng,"You're not looking at anything!")
                }
            }
            elseif(OLS[2,string] == "here"){
                stoptimer("target")
                
                TargetEnt = noentity()
                TargetPos = owner():aimPos()
                
                TargetLocked = 1
                
                printColor(ColPink,"[Target] ",ColOng,"Targetting ",ColGrn,toString(round(owner():aimPos())))
            }
            elseif(OLS[2,string] == "next"){
                FEnts:pushEntity(TargetEnt)
                target(FEnts:shiftEntity(),1)
            }
            elseif(OLS[2,string] == "off"){
                TargetEnt = noentity()
                TargetPos = vec(0)
                TargetOwner = noentity()
                
                TargetLocked = 0
                
                stoptimer("target")
                
                printColor(ColPink,"[Target] ",ColOng,"Disabling Targetting")
            }
            else{
                local Ply = findPlayerByName(OLS[2,string])
                local Ply2 = findPlayerByName(OLS[3,string])
                
                if(Ply:isPlayer()){
                    target(Ply,1)
                }
                else{
                    if(TarType:exists(OLS[2,string])){
                        findByClass(TarType[OLS[2,string],string])
                        findClipToPlayerProps(Ply2)
                        FEnts = findToArray()
                        
                        FEnt = FEnts:popEntity()
                        
                        if(FEnt:isValid()){
                            target(FEnt,1)
                        }
                        else{
                            if(Ply2:isPlayer()){
                                printColor(ColPink,"[Target] ",ColOng,"The Player ",teamColor(Ply2:owner():team()),Ply2:owner():name(),ColOng," Does not own a ",ColRed,OLS[2,string])
                            }
                            else{
                                printColor(ColPink,"[Target] ",ColOng,"Invalid Target!")
                            }
                        }
                    }
                    else{ #Find string2 type owned by string3 player
                        if(Ply2:isPlayer()){
                            findByClass(OLS[2,string])
                            findClipToPlayerProps(Ply2)
                            FEnts = findToArray()
                            
                            FEnt = FEnts:popEntity()
                            
                            if(FEnt:isValid()){
                                target(FEnt,1)
                            }
                            else{
                                if(Ply2:isPlayer()){
                                    printColor(ColPink,"[Target] ",ColOng,"The Player ",teamColor(Ply2:owner():team()),Ply2:owner():name(),ColOng," Does not own a ",ColRed,OLS[2,string])
                                    #I have no fucking clue why this still prints even when its not really a player
                                }
                                else{
                                    printColor(ColPink,"[Target] ",ColOng,"Invalid Target!")
                                }
                            }
                        }
                        else{
                            printColor(ColPink,"[Target] ",ColOng,"There is no player with the name: ",OLS[3,string])
                        }
                    }
                }
            }
        }
    }
}
